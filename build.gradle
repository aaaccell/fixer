plugins {
    id 'java-library'
    id 'maven-publish'
}

version = '0.0.1-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.0.1'
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '4.0.1'
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.6.1'
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.6.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'net.jodah:typetools:0.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

publishing {
    repositories {
        maven {
            def baseUrl = "http://sonatype-nexus3-1839447582.eu-central-1.elb.amazonaws.com/repository"
            def repositoryUrl = "$baseUrl/maven-releases/"
            def snapshotRepositoryUrl = "$baseUrl/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotRepositoryUrl : repositoryUrl
            credentials {
                username project.hasProperty('nexusUser') ? getProperty('nexusUser') : System.getenv('NEXUS_USER')
                password project.hasProperty('nexusPassword') ? getProperty('nexusPassword') : System.getenv('NEXUS_PASSWORD')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId = 'com.aaaccell'
            artifactId = 'fixer'
            pom {
                name = 'Fixer.io'
                description = 'API Wrapper'
                url = 'http://www.fixer.io'
                developers {
                    developer {
                        id = 'mjuchli'
                        name = 'Marc Juchli'
                        email = 'marcjuchli@aaaccell.ch'
                    }

                }
                scm {
                    connection = 'scm:git:git://github.com:aaaccell/fixer.git'
                    developerConnection = 'scm:git:git://github.com:aaaccell/fixer.git'
                    url = 'https://github.com/aaaccell/fixer'
                }
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
